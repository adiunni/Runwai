import Runwai.Typing
import Runwai.Gadget
import Runwai.Command
import Runwai.Tactic

#runwai_register chip Assert1(trace: [[Field: 2]: n], i : {v: UInt | v < n}) -> {Unit| trace [i][1] == Fp 2} {
  let u = assert_eq(trace [i][1], Fp 2);
  u
}

#runwai_check Assert1

#runwai_register chip IsZero(trace: [[Field: 3]: n], i : {v: UInt | v < n})
  -> {Unit| trace [i][1] == if trace [i][0] == Fp 0 then {Fp 1} else {Fp 0}} {
  let x = trace [i][0];
  let y = trace [i][1];
  let inv = trace [i][2];
  let u₁ = assert_eq(y, ((Fp 0 - x) * inv) + Fp 1);
  let u₂ = assert_eq(x*y, Fp 0);
  u₂
}

#runwai_compile_to_json IsZero

#runwai_register chip Lookup(trace: [[Field: 2]: n], i: {v: UInt | v < n}) -> {Unit| trace [i][0] == Fp 2} {
  let u = lookup Assert1(trace [i][0] : trace [i][1]);
  u
}

#runwai_prove Δ₀ Assert1 := by {
  autoTy "u"
  apply var_has_type_in_tyenv;
  apply get_update_self
  simp [Ast.nu]
  simp[Ast.renameTy]
}

#runwai_prove Δ₁ IsZero := by {
  autoTy "u₂"
  apply isZero_typing_soundness
  repeat decide
  repeat rfl
  simp[Ast.renameTy]
}

#runwai_prove Δ₂ Lookup := by {
  autoTy "u"
  repeat rfl
  apply Ty.TypeJudgment.TE_SUB
  apply var_has_type_in_tyenv
  apply get_update_self
  decide
  apply Ty.SubtypeJudgment.TSub_Refine
  apply Ty.SubtypeJudgment.TSub_Refl
  intro σ T v h₁ h₂
  unfold PropSemantics.tyenvToProp at h₁
  have h₃ := h₁ "u"
  unfold Env.getTy Env.updateTy PropSemantics.varToProp Env.getTy at h₃
  simp at h₃
  unfold Ty.lookup_pred at h₃
  have hat : (Env.getChip Δ₂ "Assert1").ident_t = "trace" := by unfold Δ₂ Env.getChip; simp
  have hai : (Env.getChip Δ₂ "Assert1").ident_i = "i" := by unfold Δ₂ Env.getChip; simp
  rw[hat, hai] at h₃
  simp [Env.freshName, Ast.renameVarinPred, Ast.renameVar] at h₃
  obtain ⟨h₄,h₅⟩ := h₃
  unfold PropSemantics.predToProp PropSemantics.exprToProp at ⊢
  apply evalProp_eq_symm at h₅
  apply evalProp_eq_trans h₅ h₄
}
